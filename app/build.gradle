apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

def getApiProperty(String key) {
    return hasProperty(key) ? getProperty(key) : System.getenv(key)
}

android {

    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {

        applicationId "com.hpedrorodrigues.imagesearch"

        minSdkVersion 15
        targetSdkVersion 24

        versionCode 1
        versionName "1.0.0"

        buildConfigField "String", "CSE_API_KEY", getApiProperty('cseApiKey')
        buildConfigField "String", "FLICKR_API_KEY", getApiProperty('flickrApiKey')
        buildConfigField "String", "IMGUR_API_CLIENT_ID", getApiProperty('imgurApiClientId')
        buildConfigField "String", "STREET_VIEW_API_KEY", getApiProperty('streetViewApiKey')
        buildConfigField "String", "BING_API_KEY", getApiProperty('bingSearchApiKey')
        buildConfigField "String", "PIXABAY_API_KEY", getApiProperty('pixabayApiKey')
        buildConfigField "String", "GIPHY_API_KEY", getApiProperty('giphyApiKey')
    }

    buildTypes {

        release {

            shrinkResources true
            useProguard true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles 'proguard-rules.pro'
        }

        debug {

            shrinkResources true
            useProguard true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {

        abortOnError false
    }
}

buildscript {

    repositories {

        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {

        classpath 'io.fabric.tools:gradle:1.21.7'
    }
}

repositories {

    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {

    // - Support Library
    compile 'com.android.support:appcompat-v7:24.1.0'
    compile 'com.android.support:design:24.1.0'
    compile 'com.android.support:cardview-v7:24.1.0'

    // - StaggeredGridView
    compile 'com.etsy.android.grid:library:1.0.5'

    // - Loading Library
    compile 'com.wang.avi:library:1.0.5'
    compile 'com.nineoldandroids:library:2.4.0'

    // - Image Loading
    compile 'com.koushikdutta.ion:ion:2.1.8'

    // - Licenses Dialog
    compile('de.psdev.licensesdialog:licensesdialog:1.8.1') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    // - Dagger 2
    compile 'com.google.dagger:dagger:2.2'
    apt 'com.google.dagger:dagger-compiler:2.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // - Lombok
    provided "org.projectlombok:lombok:1.12.6"
    apt "org.projectlombok:lombok:1.12.6"

    // - Networking dependencies
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    // - GSON
    compile 'com.google.code.gson:gson:2.4'

    // - RX
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    // - Log
    compile 'com.jakewharton.timber:timber:4.1.2'

    // - Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true
    }

    // - Analytics
    compile 'com.google.android.gms:play-services-analytics:9.0.1'

    // - Firebase
    compile 'com.google.firebase:firebase-messaging:9.0.1'

    // - Test dependencies
    testCompile 'junit:junit:4.12'
    androidTestCompile 'commons-io:commons-io:2.4'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
}

apply plugin: 'com.google.gms.google-services'